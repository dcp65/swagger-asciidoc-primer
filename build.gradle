plugins {
    id 'org.asciidoctor.jvm.convert' version '3.2.0'
    id 'org.asciidoctor.jvm.pdf' version '3.2.0'
    id 'ru.vyarus.use-python' version '2.2.0'
}

repositories {
    maven {
        url "https://repo.spring.io/release"
    }
    mavenCentral()
}

configurations {
    asciidoctorExt
}

asciidoctorj {
    version = '2.4.1'
    modules {
        diagram.use()
    }
}

dependencies {
    asciidoctorExt 'io.spring.asciidoctor:spring-asciidoctor-extensions-block-switch:0.5.0'
}

asciidoctor {
    configurations 'asciidoctorExt'
    sourceDir = file("src/asciidoc/")
    outputDir = file("${buildDir}")
    outputOptions{
        backends = ['html5', 'pdf']
        separateOutputDirs = false
    }
    baseDirFollowsSourceFile()
    sources {
        include 'index.adoc'
    }
    attributes = [
            'revnumber'         : '',
            'docinfo'           : 'shared',
            'doctype'           : 'book',
            'sectanchors'       : 'true',
            'source-highlighter': 'prettify',
            'toc'               : 'left',
            'toclevels'         : '3'
    ]
}

python.pip 'ghp-import:1.0.0'

task publishDoc(type: PythonTask) {
    group = "documentation"
    description = "Publish the generated output to GitHub Pages"
    module = 'ghp_import'
    command "-p $buildDir"
}
